apply plugin: 'com.android.application'

android {

    // private.properties contains:
    // - Unsplash API key
    // - Signin information
    //
    def privatePropertiesFile = rootProject.file("private.properties")
    Properties privateProperties = new Properties()
    privateProperties.load(new FileInputStream(privatePropertiesFile))


    signingConfigs {
        config {
            storeFile rootProject.file(privateProperties["KeyStorePath"])
            storePassword privateProperties['KeyStorePassword']
            keyAlias privateProperties['KeyAlias']
            keyPassword privateProperties['KeyPassword']
        }
    }


    compileSdkVersion 23
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "net.cchevalier.photosplash"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "UNSPLASH_API_KEY", privateProperties["UnsplashApiKey"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.chrisbanes:PhotoView:1.2.6'

    // Google Play Services: Analytics and Ads
    compile 'com.google.android.gms:play-services-analytics:9.0.0'
    compile 'com.google.android.gms:play-services-ads:9.0.0'
}

// Add the plugin to the bottom of your app-level build.gradle:
apply plugin: 'com.google.gms.google-services'
